prototype(Jonnitto.Maps:Presentation.Map) < prototype(Carbon.GeoMap:Presentation.Map) {
    @if.hasMapService = ${this.mapService && this.mapLibrary}
    @propTypes {
        mapService = ${PropTypes.oneOf(['Stadiamaps', 'GoogleMaps', 'OpenStreetMap', 'Protomaps'])}
        mapLibrary = ${PropTypes.oneOf(['MapLibre', 'Leaflet', 'GoogleMapsLibrary'])}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
    }

    mapLibrary = ${Configuration.setting('Jonnitto.Maps.mapLibrary')}
    mapService = ${Configuration.setting('Jonnitto.Maps.mapService')}

    effect = ${Configuration.Setting('Jonnitto.Maps.effect')}

    class = ${BEM.modifier('jonnitto-maps', [String.toLowerCase(this.mapLibrary), String.toLowerCase(this.mapService)])}
    aspectRatio = ${Configuration.Setting('Jonnitto.Maps.aspectRatio')}
    js = ${StaticResource.uri('Jonnitto.Maps', 'Public/Scripts/' + this.mapService + (this.mapService == 'GoogleMaps' ? '' : '-' + this.mapLibrary) + '.js')}
    css = ${StaticResource.uri('Jonnitto.Maps', 'Public/Styles/' + (this.mapService == 'GoogleMaps' ? 'GoogleMaps' : this.mapLibrary) + '.css')}
}
